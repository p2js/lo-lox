    program -> declaration* EOF ;

declaration -> funDecl | varDecl | statement ;
    varDecl -> "var" IDENTIFIER ("=" expression)? ";" ;
    funDecl -> "fun" function ;
   function -> IDENTIFIER "(" parameters? ")" block ;
    

 parameters -> IDENTIFIER ("," IDENTIFIER)* ;

  statement -> exprStmt | ifStmt | forStmt | whileStmt | printStmt | returnStmt | block ;

      block -> "{" declaration* "}" ;

   exprStmt -> expression ";" ;
     ifStmt -> "if" "(" expression ")" statement ( "else" statement )? ; 
    forStmt -> "for" "(" (varDecl | exprStmt | ";") expression? ";" expression? ")" statement ;
  whileStmt -> "while" "(" expression ")" statement ;
  printStmt -> "print" expression ";" ;
 returnStmt -> "return" expression? ";" ;

 expression -> ternary ;
 assignment -> IDENTIFIER "=" assignment | ternary ;
    ternary -> logic_or ('?' ternary ":" ternary)? ;
   logic_or -> logic_and ("or" logic_and)* ;
  logic_and -> equality ("and" equality)* ;
   equality -> comparison ( ( '==' | '!=' ) comparison )* ;
 comparison -> term ( ( "<" | "<=" | ">" | ">=") term )* ;
       term -> factor ( ( "+" | "-" ) factor )* ;
     factor -> unary ( ( "*" | "/" ) unary)* ;
      unary -> ("!" | "-") unary | call ;
       call -> primary ( "(" arguments? ")")* ;
    primary -> NUMBER | STRING | "true" | "false" | "nil" | "(" expression ")" | IDENTIFIER;

  arguments -> expression ("," expression)* ;
